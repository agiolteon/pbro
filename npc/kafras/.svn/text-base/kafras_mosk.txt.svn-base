//===== Cronus Script ========================================
//= Kafras de Moscóvia
//===== Por: =================================================
//= eAthena Dev Team
//===== Versão Atual: ========================================
//= 1.0b
//===== Compatibilidade: =====================================
//= Cronus SVN
//===== Descrição: ===========================================
//= Kafras de Moscóvia.
//===== Comentários Adicionais: ==============================
//= 1.0 First Version. [Kisuka]
//= 1.0a Traduzido. [Puncker]
//= 1.0b Revisado. [RoM]
//============================================================

moscovia,223,191,4	script	Equipe Kafra#mosk	114,{
	cutin "kafra_04",2;
	mes "[Equipe Kafra]";
	mes "Bem-vindo à";
	mes "Corporação Kafra.";
	mes "Os serviços Kafra";
	mes "estão sempre ao seu lado.";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Salvar:Usar Armazém:Alugar um Carrinho:Outras Informações:Cancelar")) {
		case 1:
			mes "[Equipe Kafra]";
			mes "Seu Ponto de Retorno foi";
			mes "salvo na cidade de";
			mes "Moscóvia. Obrigada por";
			mes "usar o Serviço Kafra.";
			savepoint "moscovia",221,194;
			close2;
			cutin "", 255;
			end;
		case 2:
			if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
				mes "[Equipe Kafra]";
				mes "Lamento, mas você";
				mes "precisa ter as";
				mes "Habilidades Básicas no Nível 6 para";
				mes "usar o Serviço de Armazém.";
				close2;
				cutin "", 255;
				end;
			}
			if(countitem(7059)) delitem 7059,1;
			else {
				if(Zeny<80){
					mes "[Equipe Kafra]";
					mes "Lamento, mas você não";
					mes "tem zeny suficiente para usar";
					mes "o Serviço de Armazém. Nossa";
					mes "taxa de acesso ao Armazém é 80 zeny.";
					close2;
					cutin "", 255;
					end;
				}
				set Zeny, Zeny-80;
				set RESRVPTS, RESRVPTS + (80/5);
			}
			mes "[Equipe Kafra]";
			mes "Aqui, deixe-me abrir";
			mes "o seu Armazém.";
			mes "Obrigada por usar";
			mes "o Serviço Kafra.";
			
			callfunc("F_CheckKafCode");	//check your storage password, if set
			
			close2;
			openstorage;
			cutin "", 255;
			end;
		case 3:
			if(baseClass != Job_Merchant){
				mes "[Equipe Kafra]";
				mes "Lamento, mas o";
				mes "Serviço de Aluguel de Carrinho";
				mes "está disponível apenas para Mercadores,";
				mes "Ferreiros, Mestre-Ferreiros,";
				mes "Alquimistas e Criadores.";
				close2;
				cutin "", 255;
				end;
			}

			else if(checkcart() == 1){
				mes "[Equipe Kafra]";
				mes "Você já possui";
				mes "um Carrinho equipado.";
				mes "Infelizmente, nós não podemos";
				mes "alugar mais que um para";
				mes "cada cliente ao mesmo tempo.";
				close2;
				cutin "", 255;
				end;
			}

			if(countitem(7061) > 0) delitem 7061,1;
			else {
				mes "[Equipe Kafra]";
				mes "A taxa de aluguel do Carrinho";
				mes "é 700 zeny. Você gostaria";
				mes "de alugar um Carrinho?";
				next;
				if(select("Alugar um Carrinho.:Cancelar.") == 1) {
					if(Zeny<700){
						mes "[Equipe Kafra]";
						mes "Lamento, mas você";
						mes "não tem zeny suficiente";
						mes "para pagar a taxe de";
						mes "aluguel de 700 zeny.";
						close2;
						cutin "", 255;
						end;
					}
					set Zeny,Zeny-700;
					set RESRVPTS, RESRVPTS + 48;
				}else{
					close2;
					cutin "", 255;
					end;
				}
			}
			setcart;
			close2;
			cutin "", 255;
			end;
		case 4:
			if(select("Checar Pontos Reservados.:Cancelar") == 2) {
				cutin "", 255;
				close;
			}
			mes "[Equipe Kafra]";
			mes strcharinfo(0) + ", você tem um total de";
			mes RESRVPTS+ " Pontos Reservados.";
			next;
			mes "[Equipe Kafra]";
			mes "Você pode trocar seus";
			mes "Pontos Reservados por";
			mes "recompensas no Escritório Principal Kafra em Al De Baran. Por favor use nossos";
			mes "serviços para ver os benefícios de nosso programa de recompensas.";
			close2;
			cutin "", 255;
			end;
		case 5:
			mes "[Equipe Kafra]";
			mes "Nós, aqui na Corporação Kafra,";
			mes "estamos sempre nos esforçando para lhe dar os melhores serviços.";
			close2;
			cutin "", 255;
			end;
	}
}