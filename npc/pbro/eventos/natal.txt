//===== Cronus Script ========================================
//= Evento de Natal
//===== Por ==================================================
//= Banned
//===== Versão ===============================================
//= 1.5
//===== Descrição ============================================
//= Sumona Monstros Natalinos.
//===== Changelog ============================================
//= 1.0 Script inicial. [Banned]
//= 1.1 Adicionado set de mapflags. [Banned]
//= 1.2 Reformulado, coisas inúteis retiradas. [Banned]
//= 1.3 Corrigida contagem de pontos. [Banned]
//= 1.4 Script Reestruturado. [Mehah]
//= 1.5 Otimizado e algumas correções. [RoM]
//============================================================
prontera,156,172,5	script	Evento de Natal::Evento de Natal	718,{

	show "[Papai Noel]";
	show "Olá "+strcharinfo(0)+"!";
	show "O que você gostaria de fazer?";
	next;
	if(getgmlevel() > 20) set .@m$,"Manipular o Evento";
	while(1){
		switch(select(.@m$,"Contar Meus Pontos","Pegar meu Prêmio!","Cancelar")){

			case 1:
			show "[Papai Noel]";
			show "O que gostaria de fazer com o Evento?";
			next;
			switch(select("Vamos Começar!","Parar o Evento!","Cancelar")){

				Case 1:
				show "[Papai Noel]";
				show "Inciado!";
				close2;
				announce "Começa agora mais uma rodada do Evento de Natal!",8;
				set $@MapMobs,250;
				monster "prontera.gat",0,0,"Antônio",1247,50,"Evento de Natal::OnMobDead";
				monster "prontera.gat",0,0,"Cookie Natalino",1246,50,"Evento de Natal::OnMobDead";
				monster "prontera.gat",0,0,"Poring Noel",1062,50,"Evento de Natal::OnMobDead";
				monster "prontera.gat",0,0,"Presente",1249,50,"Evento de Natal::OnMobDead";
				monster "prontera.gat",0,0,"Goblin Natalino",1245,50,"Evento de Natal::OnMobDead";
				initnpctimer;
				end;

				Case 2:
				show "[Papai Noel]";
				show "Finalizado!";
				close2;
				announce "Acabou mais uma rodada do Evento de Natal, fique atento para as próximas!",8;
				stopnpctimer;
				killmonsterall "prontera.gat";
				end;

				Case 3:
				close;
				}

			case 2:
			show "[Papai Noel]";
			show "Você tem "+#Mob_Killed+" pontos!";
			next;
			break;

			case 3:
			show "[Papai Noel]";
			show "Escolha seu prêmio!";
			next;
			switch(select("1 Gorro de Natal 2.0[1] [^00FF00100 Pontos^000000]",
			"1 Chapéu de Poring Noel [^00FF0050 Pontos^000000]",
			"1 Lacinho Natalino[1] [^00FF0075 Pontos^000000]",
			"1 Chapéu de Festa [^00FF0025 Pontos^000000]",
			"10 Fogos Artifício [^00FF0010 Pontos^000000]",
			"100 Sacola de Biscoitos [^00FF0010 Pontos^000000]",
			"5 Galho Sangrento [^00FF0050 Pontos^000000]",
			"3 Bolsa do Papai Noel [^00FF0025 Pontos^000000]",
			"1 Grimorio de Combate [^00FF0010 Pontos^000000]",
			"1 Goma de Mascar [^00FF0015 Pontos^000000]",
			"Nenhum")){				
				case 1: callsub mobKillCheck,100,5384,1;
				case 2: callsub mobKillCheck,50,5381,1;
				case 3: callsub mobKillCheck,75,5382,1;
				case 4: callsub mobKillCheck,25,5060,1;
				case 5: callsub mobKillCheck,10,12018,10;
				case 6: callsub mobKillCheck,10,12130,100;						
				case 7: callsub mobKillCheck,50,12103,5;
				case 8: callsub mobKillCheck,25,12132,3;
				case 9: callsub mobKillCheck,10,14533,1;
				case 10: callsub mobKillCheck,15,12210,1;										
				}

			case 4:
			close;
		}
	}

mobKillCheck:
	show "[Papai Noel]";
	if(#Mob_Killed < getarg(0)) show "Você não tem pontos o suficiente.";
	else {
	show "Muito Bem, aí está seu prêmio!";
	additem getarg(1),getarg(2);
	set #Mob_Killed,#Mob_Killed-getarg(0);
	}
	close;

OnMobDead:
	set #Mob_Killed,#Mob_Killed+1;
	set $@MapMobs,$@MapMobs-1;
	if(!$@MapMobs){
		announce "Todos os Monstros foram Mortos, portanto o Evento acabou!",bc_blue|bc_all;
		stopnpctimer;
		}
	end;

OnTimer2000:
	announce "[Papai Noel]: Vocês tem 3 minutos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer62000:
	announce "[Papai Noel]: Vocês tem 2 minutos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer92000:
	announce "[Papai Noel]: Matandos os Monstros você ganha pontos e pode trocar por prêmios!",bc_blue|bc_all;
	end;
OnTimer122000:
	announce "[Papai Noel]: Vocês tem 1  minutos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer152000:
	announce "[Papai Noel]: Vocês só tem 30 segundos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer172000:
	announce "[Papai Noel]: Vocês só tem 10 segundos para matar todos os monstros!",bc_blue|bc_all;
	end;
OnTimer182000:
	announce "[Papai Noel]: O Tempo do Evento acabou, vá trocar seus prêmios!",bc_blue|bc_all;
	killmonsterall "prontera.gat";
	stopnpctimer;
	end;
}